#!/usr/bin/perl
use strict;
use warnings;
use Getopt::Std;
use Text::CSV_XS;

our($opt_c, $opt_d, $opt_h, $opt_l, $opt_o, $opt_q, $opt_y, $opt_z);
getopts('c:d:hilo:q:y:z:') || exit 2;
# number of lines to determine maximum field width
my $max_lines = $opt_y || 1000;
Usage() if $opt_h;

my $fh;
my $file = $ARGV[0];
if (! $file or $file eq '-') {
	$fh = *STDIN;
} else {
	open $fh, $file or die "$file: $!";
}

my ($sep, $lines) = get_separator($max_lines, $fh, $opt_d);

my $csv = Text::CSV_XS->new({
	binary => 1,
	auto_diag => 1,
	sep_char => $sep,
	quote_char => ($opt_q or '"'),
	allow_loose_quotes => 1,
	allow_unquoted_escape => 1,
	allow_whitespace => 1,
	allow_loose_escapes => 1});

process_csv($csv, $sep, $lines);

sub Usage {
	(my $prog = $0) =~ s,.*/,,;
	warn <<EOF;
Usage: $0 [options] [filename]
    Tabular view of a csv file from a file or STDIN. Tries to be as tolerant
    as possible: e.g. mixed latin1/utf-8 chars, guessing delimiter for csv
    fields (, ; | : and TAB), guessing field width, tolerates bad quoting,
    lines with different number of fields, missing separator char in input etc.
    Not yet extensively tested, use at own risk.
Options:
    -c columns      display only given columns (number or range, e.g. 1,3-5)
    -d delimiter    overrides the guessed delimiting character
    -l              add a first column containing the line number
    -h              print this help text
    -o string       field separator in output, e.g. ' | ' or ' '
    -q quote_char   override the default quote character (")
    -y #_of_lines   # of lines to determine field width (1000), 0 for unlimited
    -z max_fld_size maximum field width, wrap (+num) or truncate ('-num')
EOF
	exit 0;
}

sub get_separator {
	my ($max_lines, $fh, $fixed_sep) = @_;
	my $lines;
	# regex taken from Text-CSV-Separator by Enrique Nell and slightly modified
	my $numrxdot = qr/(?:^|(?<=[^\d,.]))
		(?:[-+]?(?:\d{0,3}?(?:\.\d{3})*|\d+),\d+)(?=$|[^\d,.])/x;
	my $numrxcomma = qr/(?:^|(?<=[^\d,.]))
		(?:[-+]?\d{0,3}?((?:,\d{3})+)(?:\.\d+)?)(?=$|[^\d,.])(?:\.\d+)?/x;
	my %seps;
	$seps{$fixed_sep} = 1 if $fixed_sep;
	while (<$fh>) {
		utf8::upgrade($_);
		push @$lines, $_;
		last if $max_lines and $. > $max_lines;
		next if keys %seps;
		for my $sep ("\t", ';', '|', ':', ',') {
			my @match = /\Q$sep/g;
			$seps{$sep} = @match if @match;
		}
		if (exists $seps{','} and keys %seps > 1) {
			# check for numbers containing comma, if ambig separator
			my @nums = $_ =~ /$numrxdot/g;
			$seps{','} -= @nums;
			@nums = $_ =~ /$numrxcomma/g;
			$seps{','} -= $_ =~ tr/,/,/ for @nums;
			delete $seps{','} if $seps{','} <= 0;
		}
	}
	#prefer tab, suppress colon in case of ambiguities
	delete $seps{':'} if keys %seps > 1;
	if (exists $seps{"\t"}) {
		$sep = "\t";
	} else {
		$sep = (sort {$seps{$b}>$seps{$a}} keys %seps)[0];
	}
	return ($sep || ',', $lines);
}

sub process_csv {
	my ($csv, $sep, $lines) = @_;
	my (@size, @isnum, @rows, @selrow);
	# calculate columns to use for display
	if ($opt_c) {
		$opt_c =~ s/\s//g;
		my @ranges = split /,/, $opt_c;
		for (grep{/-/} @ranges) {
			my ($beg, $end) = split /-/;
			$selrow[$_] = 1 for ($beg .. $end);
		}
		$selrow[$_] = 1 for grep{/^\d+$/} @ranges;
	}
	#print first lines without separator character
	for my $line (@$lines) {
		last if $line =~ /$sep/;
		print $line;
		shift @$lines;
	}
	# determine field length and field type
	for my $line (@$lines) {
		next if ! $csv->parse($line);
		my @row = $csv->fields();
		my $i = 0; my $j = 1;
		my $fields;
		for my $fld (@row) {
			next if $opt_c and ! $selrow[$j++];
			$isnum[$i] = 1 if ! defined $isnum[$i];
			$fld =~ s/^\s+//; $fld =~ s/\s+$//;
			$isnum[$i] = 0 if $fld and $fld !~ /^["']?[-+.,\d]*["']?$/;
			$size[$i] = utflen($fld) if ($size[$i] || 0) <= utflen($fld);
			$size[$i] = abs($opt_z) if $opt_z and abs($opt_z) < $size[$i];
			push @$fields, $fld;
			$i++;
		}
		undef @isnum if $line eq $lines->[0];
		die "no data to display\n" if ! defined $fields;
		push @rows, $fields;
	}
	# do we have a header line
	my $has_header = 1;
	for (@{$rows[0]}) {
		# header should not contain numbers
		$has_header = 0 if /^[-+.,\s\d]+$/;
	}
	my ($tline, $sepchr);
	my $numsz = ($. < $max_lines) ? length $. : 4;
	if (! defined $opt_o) {
		my @boxc = ([qw(┌ ─ ┬ ┐)], [qw(┏ ━ ┳ ┓)]);
		my $chr = $boxc[$has_header];
		my $hline = $chr->[0];
		$tline = '└';
		if ($opt_l) {
			$hline .= $chr->[1] x ($numsz + 2) . $chr->[2];
			$tline .= '─' x ($numsz + 2) . '┴';
		}
		for (my $i=0; $i < @size; $i++) {
			$hline .= $chr->[1]x($size[$i]+2) . ($i < $#size ? $chr->[2] : '');
			$tline .= '─'x($size[$i]+2) . ($i < $#size ? '┴' : '');
		}
		print "$hline$chr->[3]\n";
		$sepchr = ' │ ';
	} else {
		$sepchr = $opt_o;
	}
	if ($has_header) {
		my $tline = '┡';
		$tline .= '━' x ($numsz + 2) . '╇' if $opt_l;
		my $sepchr = $opt_o;
		if ( ! defined $opt_o) {
			$sepchr = ' ┃ ';
			$tline .= '━'x($_+2) . '╇' for @size;
			substr($tline, -3, 3, '');
		}
		print_line(shift @rows, \@size, \@isnum, $sepchr, ' 'x($numsz -1) . 1);
		print "$tline┩\n" if ! defined $opt_o;
	}
	my $lineno = 2;
	print_line($_, \@size, \@isnum, $sepchr, sprintf("%${numsz}d", $lineno++)) for @rows;
	while (<$fh>) {
		utf8::upgrade($_);
		next if ! $csv->parse($_);
		my @row = $csv->fields();
		my $fields;
		for (my $i=0; $i < @row; $i++) {
			push @$fields, $row[$i] if $selrow[$i+1] or ! $opt_c;
		}
		print_line($fields, \@size, \@isnum, $sepchr, sprintf("%${numsz}d", $.));
	}
	close $fh;
	print "$tline┘\n" if ! defined $opt_o;
}

sub print_line {
	# print with UTF has some problems, use a dirty hack
	my ($str, $size, $isnum, $sep, $lnum) = @_;
	push @$str, '' while @$str < @$size;
	s/^\s+// for @$str; s/\s+$// for @$str;
	while ("@$str" !~ /^\s*$/) {
		my @h2;
		for (my $i = 0; $i < @$str; $i++) {
			my $len = utflen($str->[$i], $size->[$i]);
			$h2[$i] = substr $str->[$i], 0, $len, '';
			my $num = $size->[$i] - utflen($h2[$i]);
			$h2[$i] .= " " x $num if $num > 0 and ! $isnum->[$i];
			$h2[$i] = " " x $num . $h2[$i] if $num > 0 and $isnum->[$i];
		}
		(my $sep1 = $sep) =~ s/^\s//g;
		(my $sep2 = $sep) =~ s/\s$//g;
		unshift @h2, $lnum if $opt_l;
		print $sep1 . join($sep, @h2) . $sep2 . "\n";
		last if $opt_z and $opt_z < 0;
	}
}

sub utflen {
	my ($str, $len) = @_;
	my @bytes = split '', $str;
	my $chrlen = $len || @bytes;
	my $l = 0;
	my $chars = 0;
	while ($chrlen-- and $l < @bytes) {
		$l += unpack('%B4', $bytes[$l - 1]) - 1 if ord($bytes[$l++]) > 191;
		$chars++;
	}
	return $len ? $l : $chars;
}
